plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.10'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
}

group 'net.seesharpsoft.intellij.plugins'
version '1.6.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version System.getenv().getOrDefault('IDEA_VERSION', 'IC-192.5728.98')
    updateSinceUntilBuild false
    instrumentCode true
    downloadSources false
}
patchPluginXml {
    changeNotes """
<pre style="font-family: sans-serif">
[FIX] When using navigate ctrl-F12 or F12 it will leaves the explorer focused #43
[FIX] Using keyboard arrow keys to navigate Project pane, File Preview grabs focus on second file visited #48
[FIX] Project panel auto-hides after file selecting #50
[NEW] keep collapse/expand state of folders in project view
</pre>
      """
}
publishPlugin {
    token = System.getenv().getOrDefault('JI_TOKEN', '')
    channels = [System.getenv().getOrDefault('JI_CHANNELS', 'Testing')]
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    jacoco {
        enabled = true
    }
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
