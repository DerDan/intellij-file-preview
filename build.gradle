plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.9'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id "com.github.ManifestClasspath" version "0.1.0-RELEASE"
}

group 'net.seesharpsoft.intellij.plugins'
version '1.3.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version System.getenv().getOrDefault('IDEA_VERSION', 'IC-191.5701.16')
    updateSinceUntilBuild false
    instrumentCode true
}
patchPluginXml {
    changeNotes """
<pre style="font-family: sans-serif">
[NEW] Note of "Autoscroll to source" behavior conflict #17
[FIX] Cannot set plugin preferences #18
[FIX] ESC keyboard shortcut should only ever close Preview windows #19
</pre>
      """
}
publishPlugin {
    username = System.getenv().getOrDefault('JI_USER', '')
    password = System.getenv().getOrDefault('JI_PASSWORD', '')
    channels = [System.getenv().getOrDefault('JI_CHANNELS', 'Testing')]
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    jacoco {
        enabled = true
    }
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
